buildscript {
    ext.local = new Properties()
    if (project.rootProject.file('local.properties').exists()) {
        ext.local.load(project.rootProject.file('local.properties').newDataInputStream())
    }
}

plugins {
    id "org.jetbrains.kotlin.jvm" version "1.2.21"
    id "com.jfrog.bintray" version "1.7.3"
}

apply plugin: 'maven-publish'

defaultTasks 'clean', 'build'

group 'com.github.vokorm'
version '0.3-SNAPSHOT'

repositories {
    mavenCentral()
    jcenter()
    maven { url 'https://dl.bintray.com/mvysny/github' }
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.2.21"

    // logging
    compile 'org.slf4j:slf4j-api:1.7.25'

    // db
    compile 'org.sql2o:sql2o:1.5.4'
    compile "com.zaxxer:HikariCP:2.7.8"

    // tests
    testCompile("com.github.mvysny.dynatest:dynatest:0.6")
    testCompile 'com.google.code.gson:gson:2.8.2'
    testCompile 'ch.qos.logback:logback-classic:1.2.3'
    testCompile 'com.h2database:h2:1.4.196'
}

task sourceJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId project.group
            artifactId 'vok-orm'
            version project.version
            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'A very simple persistence framework, built on top of Sql2o')
                root.appendNode('name', 'VoK-ORM')
                root.appendNode('url', 'https://github.com/mvysny/vok-orm')
            }
            from components.java
            artifact sourceJar {
                classifier "sources"
            }
        }
    }
}

build.dependsOn publishToMavenLocal

bintray {
    user = local.getProperty('bintray.user')
    key = local.getProperty('bintray.key')
    pkg {
        repo = 'github'
        name = 'com.github.vokorm'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/mvysny/vok-orm'
        publish = true
        publications = ['mavenJava']
        version {
            name = project.version
            released = new Date()
        }
    }
}

test {
    useJUnitPlatform()
}
